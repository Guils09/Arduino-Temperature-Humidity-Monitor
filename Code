#include <LiquidCrystal.h>
#include <DHT.h>
#include <math.h>


LiquidCrystal lcd(8, 7, 6, 5, 4, 3); // RS, E, D4, D5, D6, D7


#define DHTPIN 2
#define DHTTYPE DHT11
DHT dht(DHTPIN, DHTTYPE);


#define TERMISTOR A0
#define RESISTOR 10000.0
#define NOMINAL_RESISTANCE 10000.0
#define NOMINAL_TEMPERATURE 25.0
#define B_COEFFICIENT 3950.0


#define LED_HUM_GREEN 10
#define LED_HUM_RED 11
#define LED_TEMP_GREEN 12
#define LED_TEMP_RED 13

void setup() {
  lcd.begin(16, 2);
  dht.begin();
  lcd.print("Sistema OK");
  
  pinMode(LED_HUM_GREEN, OUTPUT);
  pinMode(LED_HUM_RED, OUTPUT);
  pinMode(LED_TEMP_GREEN, OUTPUT);
  pinMode(LED_TEMP_RED, OUTPUT);

  delay(2000);
  lcd.clear();
}

void loop() {
  // Ler DHT11
  float hum = dht.readHumidity();
  float tempDHT = dht.readTemperature();

  int adc = analogRead(TERMISTOR);
  float resistance = RESISTOR / ((1023.0 / adc) - 1.0);
  float tempNTC = resistance / NOMINAL_RESISTANCE;
  tempNTC = log(tempNTC);
  tempNTC /= B_COEFFICIENT;
  tempNTC += 1.0 / (NOMINAL_TEMPERATURE + 273.15);
  tempNTC = 1.0 / tempNTC;
  tempNTC -= 273.15;


  lcd.setCursor(0, 0);
  lcd.print("T:");
  lcd.print(tempDHT, 1);
  lcd.print("C H:");
  lcd.print(hum, 0);
  lcd.print("%  ");

  lcd.setCursor(0, 1);
  lcd.print("NTC:");
  lcd.print(tempNTC, 1);
  lcd.print("C   ");

  if(hum <=70.0){
    digitalWrite(LED_HUM_GREEN, HIGH);
    digitalWrite(LED_HUM_RED, LOW);
  } else {
    digitalWrite(LED_HUM_GREEN, LOW);
    digitalWrite(LED_HUM_RED, HIGH);
  }

 
  if(tempNTC >= 20.0 && tempNTC <= 30.0){
    digitalWrite(LED_TEMP_GREEN, HIGH);
    digitalWrite(LED_TEMP_RED, LOW);
  } else {
    digitalWrite(LED_TEMP_GREEN, LOW);
    digitalWrite(LED_TEMP_RED, HIGH);
  }

  delay(2000);
}

